10 REM ****************************************************
20 REM **  COPYRIGHT (c) APERTURE LABORATORIES 1985-1987 **
30 REM **    (MAINTAINER: DOUG RATTMANN, EXT. 3270)      **
40 REM ****************************************************
50 F$="APERTURE":DIM MX%(9):DIM MY%(9):DIM MT$(9):DIM MF$(10)
60 KEY OFF:SCREEN 1,0:CLS:X%=0:Y%=199:P%=0:L%=0:I%=0:R%=0:SK%=0:SN%=0
70 FOR N%=1 TO 9:MX%(N%)=0:MY%(N%)=0:MT$(N%)="":MF$(N%)="":NEXT N%
80 OPEN "DATA\"+F$+".AMF" FOR INPUT AS #1:REM OBTAIN MENU DISKS FROM OTHER ACCOUNT
90 LINE INPUT#1,T$:IF T$<>"APERTURE MENU FORMAT (c) 1985" THEN PRINT "ERR: Not a valid menu file!":END
100 INPUT#1,CB%,CP%,CG%:COLOR CB%,CP%:N%=1
110 IF EOF(1) OR N%>9 THEN CLOSE(1):GOTO 120 ELSE INPUT#1,MX%(N%),MY%(N%),MT$(N%),MF$(N%):N%=N%+1:GOTO 110
120 OPEN "DATA\"+F$+".APF" FOR INPUT AS #2:LINE INPUT#2,T$:IF T$<>"APERTURE IMAGE FORMAT (c) 1985" THEN PRINT "ERR: Not a valid image file!":END ELSE INPUT#2,SK%
130 IF EOF(2) THEN CLOSE(2):GOTO 210 ELSE LINE INPUT#2,D$:L%=LEN(D$)
140 FOR P%=1 TO L%:R%=ASC(MID$(D$,P%,1))-32:IF I%=1 THEN I%=0 ELSE I%=1
150 WHILE R%>0:IF I%=0 GOTO 180
160 IF X%+R%>320 THEN LINE(X%,Y%)-(320,Y%),CG%:R%=(R%+X%)MOD 320:X%=0:Y%=Y%-SK% ELSE LINE(X%,Y%)-(X%+R%-1,Y%),CG%:X%=X%+R%:R%=0
170 GOTO 190
180 IF X%+R%>320 THEN R%=(R%+X%)MOD 320:X%=0:Y%=Y%-SK% ELSE X%=X%+R%:R%=0
190 IF Y%<0 THEN SN%=SN%+1:Y%=199-SN%
200 WEND:NEXT P%:GOTO 130
210 FOR N%=1 TO 9:IF LEN(MT$(N%))>0 THEN LOCATE MY%(N%),MX%(N%):PRINT STR$(N%)+": "+MT$(N%);
220 NEXT N%
230 K$=INKEY$:IF LEN(K$)=0 THEN GOTO 230 ELSE I%=ASC(K$)-48
240 IF I%>=1 AND I%<=9 THEN IF LEN(MT$(I%))>0 THEN F$=MF$(I%):IF F$="EXIT" THEN SYSTEM ELSE GOTO 60
250 GOTO 230